load("@aspect_bazel_lib//lib:expand_template.bzl", "expand_template")
load("@gazelle//:def.bzl", "gazelle_binary")
load("@npm//:defs.bzl", "npm_link_all_packages")
load("@py_deps//:requirements.bzl", "all_whl_requirements")
load("@rules_python//python:pip.bzl", "compile_pip_requirements")
load("@rules_python_gazelle_plugin//manifest:defs.bzl", "gazelle_python_manifest")
load("@rules_python_gazelle_plugin//modules_mapping:def.bzl", "modules_mapping")

compile_pip_requirements(
    name = "requirements",
    extra_args = ["--allow-unsafe"],
    requirements_in = "requirements.txt",
    requirements_txt = "requirements.lock.txt",
)

# A stamp file for pushing container images.
# Use the value of --embed_label under --stamp, otherwise use a deterministic constant
# value to ensure cache hits for actions that depend on this.
expand_template(
    name = "stamped",
    out = "_stamped.tags.txt",
    stamp_substitutions = {"0.0.0": "{{BUILD_EMBED_LABEL}}"},
    template = [
        "0.0.0",
        "latest",
    ],
    visibility = ["//visibility:public"],
)

# To update pnpm-lock.yaml, run:
# bazel run -- @pnpm//:pnpm --dir $PWD install --lockfile-only
npm_link_all_packages(name = "node_modules")

# This rule fetches the metadata for python packages we depend on. That data is
# required for the gazelle_python_manifest rule to update our manifest file.
modules_mapping(
    name = "modules_map",
    wheels = all_whl_requirements,
)

# Gazelle python extension needs a manifest file mapping from
# an import to the installed package that provides it.
# This macro produces two targets:
# - //:gazelle_python_manifest.update can be used with `bazel run`
#   to recalculate the manifest
# - //:gazelle_python_manifest.test is a test target ensuring that
#   the manifest doesn't need to be updated
gazelle_python_manifest(
    name = "gazelle_python_manifest",
    modules_mapping = ":modules_map",
    # This is what we called our `pip_parse` rule, where third-party
    # python libraries are loaded in BUILD files.
    pip_repository_name = "py_deps",
)

# gazelle:prefix github.com/shaldengeki/monorepo
# gazelle:proto file
# gazelle:go_grpc_compilers @rules_go//proto:go_grpc

# gazelle:python_generation_mode file
# gazelle:map_kind py_proto_library py_proto_library @protobuf//bazel:py_proto_library.bzl

# Resolve the bazel runfiles library.
# gazelle:resolve py python.runfiles @rules_python//python/runfiles

# Manual resolves for python protobuf generation.
# This is required because the Gazelle python extension lacks native protobuf support,
# unlike the golang extension, which does this for you.
# See https://github.com/bazel-contrib/rules_python/issues/2994.
# gazelle:resolve py ark_nova_stats.bga_log_parser.proto.game_pb2 //ark_nova_stats/bga_log_parser/proto:game_proto_py_pb2
# gazelle:resolve py ark_nova_stats.bga_log_parser.proto.stats_pb2 //ark_nova_stats/bga_log_parser/proto:stats_proto_py_pb2
# gazelle:resolve py ark_nova_stats.bga_log_parser.proto.ratings_pb2 //ark_nova_stats/bga_log_parser/proto:ratings_proto_py_pb2

gazelle_binary(
    name = "gazelle",
    languages = [
        "@gazelle//language/proto",
        "@rules_python_gazelle_plugin//python",
        "@gazelle//language/go",
    ],
)
