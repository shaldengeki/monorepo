load("@aspect_bazel_lib//lib:transitions.bzl", "platform_transition_filegroup")
load("@py_deps//:requirements.bzl", "requirement")
load("@rules_oci//oci:defs.bzl", "oci_push", "oci_tarball")
load("@rules_python//python:defs.bzl", "py_binary", "py_library")
load("//src/python:py_layer.bzl", "py_oci_image")

py_library(
    name = "api_lib",
    srcs = glob(["**/*.py"]),
    imports = [".."],
    visibility = ["//:__subpackages__"],
    deps = [
        "//src/python/fitbit_challenges:config_py",
        "//src/python/fitbit_challenges:models_py",
        requirement("Flask"),
        requirement("Flask-Migrate"),
        requirement("graphql_server"),
    ],
)

py_binary(
    name = "binary",
    srcs = ["__main__.py"],
    imports = [".."],
    main = "__main__.py",
    deps = [
        ":api_lib",
        "//src/python/fitbit_challenges/scripts:wait_for_postgres",
    ],
)

# Construct an image that has three layers.
# The result will look like this:
#    bazel run //src/python/fitbit_challenges/api:tarball
#
#2f2353bd5bea: Loading layer [==================================================>]  47.13MB/47.13MB
#f02532d45017: Loading layer [==================================================>]   3.62MB/3.62MB
#9296e9071c11: Loading layer [==================================================>]  16.24kB/16.24kB
py_oci_image(
    name = "image",
    base = "@python3_image",
    binary = ":binary",
    entrypoint = ["/src/python/fitbit_challenges/api/binary"],
    env = {
        "FLASK_APP": "app.py",
        "FLASK_DEBUG": "True",
        "API_PORT": "5000",
        "FRONTEND_PROTOCOL": "http",
        "FRONTEND_HOST": "frontend",
        "FRONTEND_PORT": "5001",
        "DB_HOST": "pg",
        "DB_USERNAME": "admin",
        "DB_PASSWORD": "development",
        "DATABASE_NAME": "api_development",
        "FITBIT_CLIENT_ID": "testing",
        "FITBIT_CLIENT_SECRET": "testing",
        "FITBIT_VERIFICATION_CODE": "testing",
        "FLASK_SECRET_KEY": "testing",
    },
)

platform(
    name = "aarch64_linux",
    constraint_values = [
        "@platforms//os:linux",
        "@platforms//cpu:aarch64",
    ],
)

platform(
    name = "x86_64_linux",
    constraint_values = [
        "@platforms//os:linux",
        "@platforms//cpu:x86_64",
    ],
)

platform_transition_filegroup(
    name = "platform_image",
    srcs = [":image"],
    target_platform = select({
        "@platforms//cpu:arm64": ":aarch64_linux",
        "@platforms//cpu:x86_64": ":x86_64_linux",
    }),
)

# $ bazel run //src/python/fitbit_challenges/api:tarball
# $ docker run --rm shaldengeki/fitbit-challenges-api:latest
oci_tarball(
    name = "tarball",
    image = ":platform_image",
    repo_tags = ["shaldengeki/fitbit-challenges-api:latest"],
)

oci_push(
    name = "dockerhub",
    image = ":platform_image",
    repository = "docker.io/shaldengeki/fitbit-challenges-api",
    remote_tags = ["latest"],
)
