load("@py_deps//:requirements.bzl", "requirement")
load("@rules_python//python:defs.bzl", "py_binary", "py_library")
load("//tools/build_rules:cross_platform_image.bzl", "cross_platform_image")
load("//tools/build_rules:py_layer.bzl", "py_oci_image")

py_library(
    name = "api_lib",
    srcs = glob(["**/*.py"]),
    imports = [".."],
    visibility = ["//skeleton/api:__subpackages__"],
    deps = [
        "//skeleton:config_py",
        "//skeleton:models_py",
        requirement("Flask"),
        requirement("Flask-Migrate"),
        requirement("graphql_server"),
    ],
)

py_binary(
    name = "binary",
    srcs = ["__main__.py"],
    imports = [".."],
    main = "__main__.py",
    deps = [
        ":api_lib",
        "//scripts:wait_for_postgres",
    ],
)

py_oci_image(
    name = "base_image",
    base = "@python3_image",
    binary = ":binary",
    cmd = [
        "/skeleton/api/binary.runfiles/_main/scripts/wait_for_postgres",
        "/skeleton/api/binary",
    ],
    env = {
        "FLASK_APP": "app.py",
        "FLASK_DEBUG": "True",
        "API_PORT": "5000",
        "FRONTEND_PROTOCOL": "http",
        "FRONTEND_HOST": "frontend",
        "FRONTEND_PORT": "5001",
        "DB_HOST": "pg",
        "DB_USERNAME": "admin",
        "DB_PASSWORD": "development",
        "DATABASE_NAME": "api_development",
        "FLASK_SECRET_KEY": "testing",
    },
    visibility = ["//skeleton/api:__subpackages__"],
)

# $ bazel run //skeleton/api:image_tarball
# $ docker run --rm shaldengeki/skeleton-api:latest
cross_platform_image(
    name = "image",
    image = ":base_image",
    repo_tags = ["shaldengeki/skeleton-api:latest"],
    repository = "docker.io/shaldengeki/skeleton-api",
    visibility = ["//skeleton/api:__subpackages__"],
)
