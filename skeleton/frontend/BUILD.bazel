load("@aspect_bazel_lib//lib:copy_file.bzl", "copy_file")
load("@aspect_bazel_lib//lib:copy_to_bin.bzl", "copy_to_bin")
load("@aspect_rules_jest//jest:defs.bzl", "jest_test")
load("@aspect_rules_ts//ts:defs.bzl", "ts_project")
load("@npm//:@tailwindcss/cli/package_json.bzl", tailwindcss_bin = "bin")
load("//tools/build_rules:frontend_image.bzl", "BUILD_DEPS", "frontend_image")

exports_files([
    "src/logo192.png",
    "src/setupTests.jsx",
    "globals.d.ts",
])

# Dependencies required to do typechecks.
TYPE_DEPS = [
    "//:node_modules/@types/jest",
    "//:node_modules/@types/lodash",
    "//:node_modules/@types/node",
    "//:node_modules/@types/react",
    "//:node_modules/@types/react-dom",
]

# Dependencies required to run tests.
TEST_DEPS = [
    "//:node_modules/@apollo/react-testing",
    "//:node_modules/@testing-library/jest-dom",
    "//:node_modules/@testing-library/react",
    "//:node_modules/jest-environment-jsdom",
    "//:node_modules/jsdom",
]

# Compiles our CSS using Tailwind styles.
# Uses the tailwindcss binary from npm.
tailwindcss_bin.tailwindcss(
    name = "tailwindcss",
    srcs = [
        "src/App.src.css",
        ":tailwind.config.js",
        ":ts",
        "//:node_modules/tailwindcss",
    ],
    outs = [
        "src/App.css",
    ],
    args = [
        "--config skeleton/frontend/tailwind.config.js",
        "-i skeleton/frontend/src/App.src.css",
        "-o skeleton/frontend/src/App.css",
    ],
    log_level = "debug",
)

# Compiles Typescript to regular old Javascript.
ts_project(
    name = "ts",
    srcs = glob(
        include = [
            "src/**/*.tsx",
            "src/**/*.ts",
            "src/**/*.jsx",
            "src/**/*.js",
        ],
    ),
    allow_js = True,
    assets = [
        "src/logo192.png",
        "src/App.src.css",
        "src/logo.svg",
    ],
    composite = True,
    declaration = True,
    out_dir = ".",
    resolve_json_module = True,
    transpiler = "tsc",
    tsconfig = "//skeleton/frontend:tsconfig",
    visibility = ["//visibility:public"],
    deps = BUILD_DEPS + TEST_DEPS + TYPE_DEPS + [
        "//skeleton/frontend/src/components:ExampleComponent",
        "//skeleton/frontend/src/components:NavBar",
        "//skeleton/frontend/src/components:PageContainer",
        "//skeleton/frontend/src/views:ExampleView",
    ],
)

frontend_image(
    name = "production",
    srcs = glob(["public/**/*"]) + [
        ":tailwindcss",
        ":ts",
    ],
    build_env = {
        "REACT_APP_API_HOST": "api.skeleton.ouguo.us",
        "REACT_APP_API_PORT": "443",
        "REACT_APP_API_PROTOCOL": "https",
    },
    docker_hub_repository = "docker.io/shaldengeki/skeleton-frontend",
    node_modules = "//:node_modules",
    repo_tags = ["shaldengeki/skeleton-frontend:latest"],
    server_name = "skeleton.ouguo.us",
    webpack_conf = ":webpack.config.js",
)

frontend_image(
    name = "development",
    srcs = glob(["public/**/*"]) + [
        ":tailwindcss",
        ":ts",
    ],
    build_env = {
        "REACT_APP_API_HOST": "localhost",
        "REACT_APP_API_PORT": "5000",
        "REACT_APP_API_PROTOCOL": "http",
    },
    docker_hub_repository = "docker.io/shaldengeki/skeleton-frontend",
    node_modules = "//:node_modules",
    repo_tags = ["shaldengeki/skeleton-frontend:latest"],
    server_name = "skeleton.ouguo.us",
    webpack_conf = ":webpack.config.js",
)

# Runs frontend Jest tests.
copy_file(
    name = "setupTests",
    src = "src/setupTests.js",
    out = "setupTests.js",
)

jest_test(
    name = "test",
    config = "jest.config.json",
    data = [
        ":setupTests",
        ":testfixtures",
        ":ts",
    ],
    node_modules = "//:node_modules",
    node_options = [
        "--experimental-vm-modules",
    ],
)

copy_to_bin(
    name = "globalsdts",
    srcs = ["src/globals.d.ts"],
    visibility = ["//visibility:public"],
)

copy_to_bin(
    name = "testfixtures",
    srcs = [
        "mocks/fileMock.js",
        "package.json",
        "src/setupTests.js",
    ],
    visibility = ["//visibility:public"],
)

copy_to_bin(
    name = "tsconfig",
    srcs = ["tsconfig.json"],
    visibility = ["//visibility:public"],
)

copy_to_bin(
    name = "jestconfig",
    srcs = ["jest.config.json"],
    visibility = ["//visibility:public"],
)
