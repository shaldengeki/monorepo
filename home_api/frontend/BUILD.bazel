load("@aspect_rules_jest//jest:defs.bzl", "jest_test")
load("@aspect_rules_ts//ts:defs.bzl", "ts_project")
load("@npm//:tailwindcss/package_json.bzl", tailwindcss_bin = "bin")
load("//tools/build_rules:frontend_image.bzl", "BUILD_DEPS", "frontend_image")

# Dependencies required to do typechecks.
TYPE_DEPS = [
    "//:node_modules/@types/jest",
    "//:node_modules/@types/lodash",
    "//:node_modules/@types/node",
    "//:node_modules/@types/react",
    "//:node_modules/@types/react-dom",
]

# Dependencies required to run tests.
TEST_DEPS = [
    "//:node_modules/@apollo/react-testing",
    "//:node_modules/@testing-library/jest-dom",
    "//:node_modules/@testing-library/react",
    "//:node_modules/jest-environment-jsdom",
    "//:node_modules/jsdom",
]

# Compiles our CSS using Tailwind styles.
# Uses the tailwindcss binary from npm.
tailwindcss_bin.tailwindcss(
    name = "tailwindcss",
    srcs = [
        "src/App.src.css",
        ":tailwind.config.js",
        ":ts",
        "//:node_modules/tailwindcss",
    ],
    outs = [
        "src/App.css",
    ],
    args = [
        "--config home_api/frontend/tailwind.config.js",
        "-i home_api/frontend/src/App.src.css",
        "-o home_api/frontend/src/App.css",
    ],
    log_level = "debug",
)

# Compiles Typescript to regular old Javascript.
ts_project(
    name = "ts",
    srcs = glob(
        include = [
            "src/**/*.tsx",
            "src/**/*.ts",
            "src/**/*.jsx",
            "src/**/*.js",
        ],
    ) + [
        "tsconfig.json",
    ],
    allow_js = True,
    assets = [
        "src/App.src.css",
    ],
    out_dir = ".",
    resolve_json_module = True,
    transpiler = "tsc",
    deps = BUILD_DEPS + TEST_DEPS + TYPE_DEPS,
)

frontend_image(
    name = "production",
    srcs = glob(["public/**/*"]) + [
        ":tailwindcss",
        ":ts",
    ],
    build_env = {
        "REACT_APP_API_HOST": "api.fitbit.ouguo.us",
        "REACT_APP_API_PORT": "443",
        "REACT_APP_API_PROTOCOL": "https",
    },
    docker_hub_repository = "docker.io/shaldengeki/home-api-frontend",
    node_modules = "//:node_modules",
    repo_tags = ["shaldengeki/home-api-frontend:latest"],
    server_name = "home.ouguo.us",
    webpack_conf = ":webpack.config.js",
)

# Runs frontend Jest tests.
jest_test(
    name = "test",
    config = "jest.config.json",
    data = [
        "package.json",
    ] + BUILD_DEPS + TEST_DEPS + [
        "src/App.test.js",
        "src/setupTests.js",
    ],
    node_modules = "//:node_modules",
    node_options = [
        "--experimental-vm-modules",
    ],
)
