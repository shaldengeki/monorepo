load("@aspect_rules_ts//ts:defs.bzl", "ts_config", "ts_project")
load("@npm//:tailwindcss/package_json.bzl", tailwindcss_bin = "bin")
load("//tools/build_rules:frontend_image.bzl", "BUILD_DEPS", "frontend_image")

# Dependencies required to do typechecks.
TYPE_DEPS = [
    "//:node_modules/@types/jest",
    "//:node_modules/@types/lodash",
    "//:node_modules/@types/node",
    "//:node_modules/@types/react",
    "//:node_modules/@types/react-dom",
]

# Dependencies required to run tests.
TEST_DEPS = [
    "//:node_modules/@apollo/react-testing",
    "//:node_modules/@testing-library/jest-dom",
    "//:node_modules/@testing-library/react",
    "//:node_modules/jest-environment-jsdom",
    "//:node_modules/jsdom",
]

# Compiles our CSS using Tailwind styles.
# Uses the tailwindcss binary from npm.
tailwindcss_bin.tailwindcss(
    name = "tailwindcss",
    srcs = [
        "src/App.src.css",
        ":tailwind.config.js",
        ":ts",
        "//:node_modules/tailwindcss",
    ],
    outs = [
        "src/App.css",
    ],
    args = [
        "--config mc_manager/frontend/tailwind.config.js",
        "-i mc_manager/frontend/src/App.src.css",
        "-o mc_manager/frontend/src/App.css",
    ],
    log_level = "debug",
)

ts_config(
    name = "tsconfig",
    src = "tsconfig.json",
    visibility = [":__subpackages__"],
)

# Compiles Typescript to regular old Javascript.
ts_project(
    name = "ts",
    srcs = glob(
        include = [
            "src/**/*.tsx",
            "src/**/*.ts",
            "src/**/*.jsx",
            "src/**/*.js",
        ],
    ),
    allow_js = True,
    assets = [
        "src/App.src.css",
        "src/components/logo.svg",
    ],
    out_dir = ".",
    resolve_json_module = True,
    transpiler = "tsc",
    tsconfig = ":tsconfig",
    deps = BUILD_DEPS + TEST_DEPS + TYPE_DEPS,
)

frontend_image(
    name = "production",
    srcs = glob(["public/**/*"]) + [
        ":tailwindcss",
        ":ts",
    ],
    build_env = {
        "REACT_APP_API_HOST": "api.minecraft.ouguo.us",
        "REACT_APP_API_PORT": "443",
        "REACT_APP_API_PROTOCOL": "https",
    },
    docker_hub_repository = "docker.io/shaldengeki/mc-manager-frontend",
    node_modules = "//:node_modules",
    repo_tags = ["shaldengeki/mc-manager-frontend:latest"],
    server_name = "minecraft.ouguo.us",
    webpack_conf = ":webpack.config.js",
)

frontend_image(
    name = "development",
    srcs = glob(["public/**/*"]) + [
        ":tailwindcss",
        ":ts",
    ],
    build_env = {
        "REACT_APP_API_HOST": "localhost",
        "REACT_APP_API_PORT": "5000",
        "REACT_APP_API_PROTOCOL": "http",
    },
    docker_hub_repository = "docker.io/shaldengeki/mc-manager-frontend",
    node_modules = "//:node_modules",
    repo_tags = ["shaldengeki/mc-manager-frontend:latest"],
    server_name = "minecraft.ouguo.us",
    webpack_conf = ":webpack.config.js",
)
